apply plugin: 'com.android.application'
android {
    signingConfigs {
        debug {
            keyAlias 'lizhangqu'
            keyPassword 'lizhangqu'
            storeFile project.rootProject.file('debug.keystore')
            storePassword 'lizhangqu'
        }
    }
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "io.github.lizhangqu.samples"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        signingConfig signingConfigs.debug
    }
    buildTypes {
        debug {

        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    RELEASE_REPOSITORY_URL = "file://${project.rootProject.file('repo/release')}"
    SNAPSHOT_REPOSITORY_URL = "file://${project.rootProject.file('repo/snapshot')}"
    RELEASE_REPOSITORY_USERNAME = ""
    RELEASE_REPOSITORY_PASSWORD = ""
    SNAPSHOT_REPOSITORY_USERNAME = ""
    SNAPSHOT_REPOSITORY_PASSWORD = ""
}

apply plugin: 'native-compile-plugin'
nativeCompile {
    defaultClassifier 'armeabi'
}
apply plugin: 'core.publish'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'


    nativeCompile 'com.android.support:appcompat-v7:26.1.0'
    nativeCompile 'com.snappydb:snappydb-native:0.2.0:armeabi@so'
    nativeCompile 'com.snappydb:snappydb-native:0.2.0:x86@so'
    nativeCompile 'com.snappydb:snappydb-native:0.2.0:mips@so'
    nativeCompile "com.snappydb:snappydb-native:0.2.0:armeabi-v7a@so"

//    nativeCompile "io.github.lizhangqu.bundle:host:1.0.0-SNAPSHOT"
//    nativeCompile "io.github.lizhangqu.bundle:host:1.0.0-SNAPSHOT:armeabi@so"
//    nativeCompile "io.github.lizhangqu.bundle:host:1.0.0-SNAPSHOT:armeabi-v7a@so"
//    nativeCompile "io.github.lizhangqu.bundle:host:1.0.0-SNAPSHOT:arm64-v8a@so"
//    nativeCompile "io.github.lizhangqu.bundle:host:1.0.0-SNAPSHOT:x86@so"
//    nativeCompile "io.github.lizhangqu.bundle:host:1.0.0-SNAPSHOT:x86_64@so"
//    nativeCompile "io.github.lizhangqu.bundle:host:1.0.0-SNAPSHOT:mips@so"
//    nativeCompile "io.github.lizhangqu.bundle:host:1.0.0-SNAPSHOT:mips64@so"

}

project.afterEvaluate {
    project.tasks.all { Task task ->
        //寻找assembleRelease
        if (task.name.equalsIgnoreCase('assembleRelease')) {
            task.doLast {
                //寻找release的apk文件，并且已经签名过，将其拷贝成.so文件，添加到archives配置中
                project.file("$buildDir/outputs/apk").eachFileRecurse { File file ->
                    if (file.getName().startsWith("${archivesBaseName}")
                            && file.getName().endsWith(".apk")
                            && file.getName().contains("-release")
                            && !file.getName().endsWith("-unsigned.apk")) {
                        //添加到发布的待发布的配置中
                        project.artifacts {
                            archives file: file, name: "host", extension: 'so', type: 'so'
                            //必要时可发布classsifier文件，发布的文件需要group:name:version:classifier才能拉下来
                            archives classifier: 'armeabi', file: file, name: "host", extension: 'so', type: 'so'
                            archives classifier: 'armeabi-v7a', file: file, name: "host", extension: 'so', type: 'so'
                            archives classifier: 'arm64-v8a', file: file, name: "host", extension: 'so', type: 'so'
                            archives classifier: 'x86', file: file, name: "host", extension: 'so', type: 'so'
                            archives classifier: 'x86_64', file: file, name: "host", extension: 'so', type: 'so'
                            archives classifier: 'mips', file: file, name: "host", extension: 'so', type: 'so'
                            archives classifier: 'mips64', file: file, name: "host", extension: 'so', type: 'so'
                            //apk classsifier
                            archives classifier: 'apk', file: file, name: "host", extension: 'apk', type: 'apk'
                        }
                    }
                }
            }
        }
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                addFilter("host") { artifact, file ->
                    artifact.name == "host"
                }
                pom("host") {
                    groupId = 'io.github.lizhangqu.bundle'
                    artifactId = "host"
                    version = "1.0.0-SNAPSHOT"
                    packaging = 'so'
                }

            }
        }
    }
}
